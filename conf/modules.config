/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    Config file for defining DSL2 per module options and publishing paths
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    Available keys to override module options:
        ext.args   = Additional arguments appended to command in module.
        ext.args2  = Second set of arguments appended to command in module (multi-tool modules).
        ext.args3  = Third set of arguments appended to command in module (multi-tool modules).
        ext.prefix = File name prefix for output files.
----------------------------------------------------------------------------------------
*/

process {

    publishDir = [
        path: { "${params.outdir}/${task.process.tokenize(':')[-1].tokenize('_')[0].toLowerCase()}" },
        mode: params.publish_dir_mode,
        saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
    ]

    withName: '.*:FASTQ_FASTQC_UMITOOLS_FASTP:FASTQC_RAW' {
        ext.prefix = { "${meta.id}.raw" }
        ext.args   = '--quiet'
        publishDir = [
            path: { "${params.outdir}/fastqc_raw" },
            mode: "copy",
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename },
        ]
    }

    if(!params.skip_trimming) {

        withName: '.*:FASTQ_FASTQC_UMITOOLS_FASTP:FASTQC_TRIM' {
            ext.args   = '--quiet'
            ext.prefix = { "${meta.id}.trim" }
            publishDir = [
                path: { "${params.outdir}/fastqc_trim" },
                mode: "copy",
                saveAs: { filename -> filename.equals('versions.yml') ? null : filename },
            ]
        }

        withName: '.*:FASTQ_FASTQC_UMITOOLS_FASTP:FASTP' {
            ext.args   = params.extra_fastp_args ?: ''
            publishDir = [
                [
                    path: { "${params.outdir}/fastp/html" },
                    mode:  "copy",
                    pattern: "*.{html}"
                ],
                [
                    path: { "${params.outdir}/fastp/json" },
                    mode:  "copy",
                    pattern: "*.{json}"
                ],
                [
                    path: { "${params.outdir}/fastp/log" },
                    mode:  "copy",
                    pattern: "*.log"
                ],
                [
                    path: { "${params.outdir}/fastp/fail" },
                    mode:  "copy",
                    pattern: "*.fail.fastq.gz",
                    enabled: params.save_trimmed_fail
                ]
            ]
        }
    }

    withName: 'MULTIQC_GLOBAL' {
        ext.args   = { params.multiqc_title ? "--title \"$params.multiqc_title\"" : '' }
        publishDir = [
            path: { "${params.outdir}/multiqc" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    withName: 'MULTIQC_PER_LANE' {
        ext.args   = { params.multiqc_title ? "--title \"$params.multiqc_title\"" : '' }
        publishDir = [
            path: { "${params.outdir}/multiqc/lanes" },
            mode: params.publish_dir_mode,
            saveAs: {
                filename ->
                    switch (filename) {
                        case 'versions.yml':
                            null
                            break
                        case ~/\[LANE:\d+\]_multiqc_(report\.html|plots|data)/:
                            def lane = (filename =~ /\[LANE:(\d+)\]_multiqc_(report\.html|plots|data)/)[0][1]
                            def new_filename = filename.replaceFirst(
                                "(?<prefix>.*)\\[LANE:${lane}\\]_(?<suffix>multiqc_(report\\.html|plots|data).*)",
                                '${prefix}${suffix}')
                            "L${lane}/${new_filename}"
                            break
                        default:
                            filename
                    }
            }
        ]
    }

    withName: 'MULTIQC_PER_GROUP' {
        ext.args   = { params.multiqc_title ? "--title \"$params.multiqc_title\"" : '' }
        publishDir = [
            path: { "${params.outdir}/multiqc/groups" },
            mode: params.publish_dir_mode,
            saveAs: {
                filename ->
                    switch (filename) {
                        case 'versions.yml':
                            null
                            break
                        case ~/\[GROUP:.+\]_multiqc_(report\.html|plots|data)/:
                            def group = (filename =~ /\[GROUP:(.+)\]_multiqc_(report\.html|plots|data)/)[0][1]
                            def new_filename = filename.replaceFirst(
                                "(?<prefix>.*)\\[GROUP:${group}\\]_(?<suffix>multiqc_(report\\.html|plots|data).*)",
                                '${prefix}${suffix}')
                            "${group}/${new_filename}"
                            break
                        default:
                            filename
                    }
            }
        ]
    }

    withName: 'MULTIQC_PER_RUNDIR' {
        ext.args   = { params.multiqc_title ? "--title \"$params.multiqc_title\"" : '' }
        publishDir = [
            path: { "${params.outdir}/multiqc/rundirss" },
            mode: params.publish_dir_mode,
            saveAs: {
                filename ->
                    switch (filename) {
                        case 'versions.yml':
                            null
                            break
                        case ~/\[RUNDIR:.+\]_multiqc_(report\.html|plots|data)/:
                            def rundir = (filename =~ /\[RUNDIR:(.+)\]_multiqc_(report\.html|plots|data)/)[0][1]
                            def new_filename = filename.replaceFirst(
                                "(?<prefix>.*)\\[RUNDIR:${rundir}\\]_(?<suffix>multiqc_(report\\.html|plots|data).*)",
                                '${prefix}${suffix}')
                            "${rundir}/${new_filename}"
                            break
                        default:
                            filename
                    }
            }
        ]
    }
}
